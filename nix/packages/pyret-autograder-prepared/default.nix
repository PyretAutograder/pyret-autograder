{
  stdenv,
  nodejs,
  pnpm,
  pyret-autograder-src,
  lib,
  ...
}:
stdenv.mkDerivation (finalAttrs: {
  name = "workspace-prepared";

  src = pyret-autograder-src;

  nativeBuildInputs = [
    nodejs
    pnpm.configHook
  ];

  pnpmDeps =
    (pnpm.fetchDeps {
      pname = finalAttrs.name;
      inherit (finalAttrs) src;
      fetcherVersion = 2;
      # hash = lib.fakeHash;
      hash = "sha256-0imd3gMgbWmaqW1DHa5Ne/x0eQhQMi+qo4pL9Jx+xpQ=";
    }).overrideAttrs
      (old: {
        # HACK: we really shouldn't be doing this! but its tricky to update the lockfile for the overrides
        installPhase = builtins.replaceStrings [ "--frozen-lockfile" ] [ "" ] old.installPhase + ''
          cp pnpm-lock.yaml $out/.generated-pnpm-lock.yaml
        '';
      });
  postPatch = ''
    echo ">> replacing pnpm-lock.yaml with one generated by fetchDeps"
    cp ${finalAttrs.pnpmDeps}/.generated-pnpm-lock.yaml pnpm-lock.yaml
  '';

  buildPhase = ''
    set -eu

    runHook preBuild

    pnpm install --offline --frozen-lockfile

    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p $out
    cp -r . $out

    runHook postInstall
  '';
})
